apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group 'com.takwolf'
version '0.0.2'

dependencies {
    compile 'org.apache.commons:commons-lang3:3.5'
    testCompile 'junit:junit:4.11'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = 'GB2260.java'
        desc = 'The Java implementation for looking up Chinese administrative divisions.'
        websiteUrl = 'https://github.com/TakWolf/GB2260.java'
        issueTrackerUrl = 'https://github.com/TakWolf/GB2260.java/issues'
        vcsUrl = 'https://github.com/TakWolf/GB2260.java.git'
        licenses = ['MIT']
        labels = ['gb2260']
        publicDownloadNumbers = true
        githubRepo = 'TakWolf/GB2260.java'
        githubReleaseNotesFile = 'README.md'
    }
}
